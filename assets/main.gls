#version 150
precision highp float;

uniform mat4 u_modelView;

@vertex

#extension GL_ARB_explicit_attrib_location: enable

uniform mat4 u_projection;
layout(location = 0) in vec4 a_vertex;
layout(location = 1) in vec4 a_normal;
smooth out vec4 v_vertex;
flat out vec4 v_normal;

void main() {
	vec4 modelViewVertex = u_modelView * a_vertex;
	v_vertex = modelViewVertex;
	v_normal = u_modelView * a_normal;
	gl_Position = u_projection * modelViewVertex;
}

@fragment

smooth in vec4 v_vertex;
flat in vec4 v_normal;
out vec4 o_color;

void main() {
	vec3 ambient = vec3(0.01);

	vec4 vAux = vec4(0.0) - v_vertex;
	vec4 lightDir = normalize(vAux);
	float distance = length(vAux);

	float radius = 64.0;
	float attenuation = clamp(1.0 - distance * distance / (radius * radius), 0.0, 1.0);
	attenuation *= attenuation;

	float nDotL = max(0.0, dot(v_normal, lightDir));
	vec3 diffuse = vec3(1.0, 0.5, 0.0) * nDotL * attenuation;

	vec3 specular = vec3(0.0, 0.0, 0.0);
	if(nDotL > 0.0) {
		vec4 halfway = normalize(lightDir + normalize(-v_vertex));
		float nDotH = max(0.0, dot(v_normal, halfway));
		specular += pow(nDotH, 64.0) * attenuation;
	}
	vec3 final = ambient + diffuse + specular;

	vec3 hdr = 1.0 - exp2(-final * 1.5);
	hdr *= max(1.0, length(final) / 2);
	o_color = vec4(hdr, 1.0);
}
